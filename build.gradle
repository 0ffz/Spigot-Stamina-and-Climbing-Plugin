plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'io.github.0ffz.github-packages' version '1.2.0'
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

group = 'com.mineinabyss'
version = plugin_version

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven githubPackage.invoke("MineInAbyss/Idofront")
    maven githubPackage.invoke("MineInAbyss/KotlinSpice")
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly platform("com.mineinabyss:kotlinspice:$kotlin_version+")

    implementation "com.mineinabyss:idofront:0.4.3"
}

shadowJar {
    getArchiveBaseName().set("stamina-climb")
    getArchiveClassifier().set(null)

    dependencies {
        exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib:.*'))
        exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib-jdk8:.*'))
    }

    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize()
}
build.dependsOn shadowJar

publishing {
    repositories {
        maven githubPackagePublish.invoke("MineInAbyss/StaminaClimb")
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
