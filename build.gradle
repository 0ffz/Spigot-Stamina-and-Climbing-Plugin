import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'io.github.0ffz.github-packages' version '1.0.3'
}

group = 'com.mineinabyss'
version = plugin_version

if (project.hasProperty("buildNo")) version += ".$buildNo"

java {
    setSourceCompatibility(1.8)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven githubPackage.invoke("MineInAbyss/Idofront")
    maven githubPackage.invoke("MineInAbyss/KotlinSpice")
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version"
    compileOnly "org.cultofclang.minecraft:kotlinspice:$kotlin_version+"

    implementation "com.mineinabyss:idofront:0.2.1-alpha.18"
}

shadowJar {
    getArchiveBaseName().set("stamina-climb")
    getArchiveClassifier().set(null)

    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize()
}
build.dependsOn shadowJar

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/StaminaClimb")
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: [plugin_version: plugin_version]
}

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    println("Copying to plugin directory $plugin_path")
    task copyJar(type: Copy) {
        from shadowJar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
}

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }